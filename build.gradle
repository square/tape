buildscript {
  dependencies {
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.13'
  }

  repositories {
    maven { url 'https://plugins.gradle.org/m2/' }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    mavenCentral()
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Xlint:all', '-Xlint:-path', '-Werror']
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone' &&
                details.requested.name.startsWith('error_prone_')) {
          details.useVersion '2.1.2'
        }
      }
    }
  }

  apply plugin: 'net.ltgt.errorprone'
}
